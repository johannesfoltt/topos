/-
Copyright (c) 2024 Charlie Conneen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Charlie Conneen
-/
import Mathlib.CategoryTheory.ChosenFiniteProducts
-- import Mathlib.CategoryTheory.Topos.Power
import Topos.Power


/-!
# Topoi

We follow Mac Lane and Moerdijk in defining a topos as a category
with a terminal object, pullbacks, a subobject classifier, and
power objects.

In this file, a "predicate" refers to any morphism into `Î© C`.

## Main definitions

* `CategoryTheory.IsTopos C` is a typeclass asserting that `C` is a topos.

* The namespace `CategoryTheory.Topos.Predicate` provides an API for various
  useful predicates, such as `Predicate.eq` as the equality predicate
  `B â¨¯ B âŸ¶ Î© C`, and `Predicate.isSingleton` which is the classifier of the
  singleton map `singleton B : B âŸ¶ Pow B`.

## Main results

* `singleton B : B âŸ¶ Pow B` is a monomorphism. This is `singletonMono`.

## Notation

* If `Ï†` is a predicate `X âŸ¶ Î© C`, `âŒˆÏ†âŒ‰` is shorthand for `Name Ï†`,
  which is the global element `âŠ¤_ C âŸ¶ Pow X` associated to `Ï†`.

## References

* [S. MacLane and I. Moerdijk, *Sheaves in Geometry and Logic*][MLM92]

-/

namespace CategoryTheory

open Category Limits MonoClassifier Power

universe u v

variable (C : Type u) [Category.{v} C]

/-- A category is a topos if it has a terminal object,
all pullbacks, a subobject classifier, and power objects.
-/
class IsTopos where
  /-- A topos has a terminal object. -/
  [has_terminal : HasTerminal C]
  /-- A topos has pullbacks. -/
  [has_pullbacks : HasPullbacks C]
  /-- A topos has a subobject classifier. -/
  [subobject_classifier : HasClassifier C]
  /-- A topos has power objects. -/
  [has_power_objects : HasPowerObjects C]

attribute [instance] IsTopos.has_terminal IsTopos.has_pullbacks
                     IsTopos.subobject_classifier IsTopos.has_power_objects

variable [IsTopos C] {C}

namespace Topos

-- /-- A topos has (chosen) finite products. -/
-- noncomputable instance chosenFiniteProducts : ChosenFiniteProducts C :=
--   ChosenFiniteProducts.ofFiniteProducts C
/-- A topos has binary products. -/
instance hasBinaryProducts : HasBinaryProducts C :=
  hasBinaryProducts_of_hasTerminal_and_pullbacks C
/-- A topos has finite products. -/
instance hasFiniteProducts : HasFiniteProducts C :=
  hasFiniteProducts_of_has_binary_and_terminal
/-- A topos has equalizers, since it has pullbacks and
binary products.
-/
instance hasEqualizers : HasEqualizers C :=
  hasEqualizers_of_hasPullbacks_and_binary_products


noncomputable section

/-- The predicate on `B` which corresponds to the subobject `ğŸ™ B: B âŸ¶ B`. -/
abbrev Predicate.true_ (B : C) : B âŸ¶ Î© C := terminal.from B â‰« (t C)

/--
  The equality predicate on `B â¨¯ B`.
-/
abbrev Predicate.eq (B : C) : B â¨¯ B âŸ¶ Î© C := Ï‡_ (diag B)

/-- The lift `X âŸ¶ B â¨¯ B` of a morphism with itself, when composed
with `predicate.eq B`, is true.
-/
lemma Predicate.lift_eq {X B : C} (b : X âŸ¶ B) :
    prod.lift b b â‰« Predicate.eq B = Predicate.true_ X := by
  rw [â†prod.comp_diag b, assoc, comm (diag B), â†assoc, terminal.comp_from]

/-- Two maps in a topos are equal if their lift composed with
the equality predicate on `B â¨¯ B` is true.
In other words, this combined with `Predicate.lift_eq` states that
`Predicate.eq` is able to distinguish whether two morphisms are equal.
-/
lemma Predicate.eq_of_lift_eq {X B : C} {b b' : X âŸ¶ B}
  (comm' : prod.lift b b' â‰« Predicate.eq B = Predicate.true_ X) :
    b = b' := by
  dsimp only [true_] at comm'
  have t : (IsPullback.lift (isPullback _) _ _ comm') â‰« diag _ = prod.lift b b' :=
    IsPullback.lift_fst (isPullback _) _ _ comm'
  rw [prod.comp_diag] at t
  have tâ‚ := congrArg (fun k â†¦ k â‰« prod.fst) t
  have tâ‚‚ := congrArg (fun k â†¦ k â‰« prod.snd) t
  simp at tâ‚
  simp at tâ‚‚
  exact tâ‚.symm.trans tâ‚‚

/-- The "singleton" map `B âŸ¶ Pow B`.
In Set, this map sends `b âˆˆ B` to the
singleton set containing just `b`.
-/
def singleton (B : C) : B âŸ¶ pow B := (Predicate.eq B)^

/-- `singleton B : B âŸ¶ Pow B` is a monomorphism. -/
instance singletonMono (B : C) : Mono (singleton B) where
  right_cancellation := by
    intro X b b' h
    rw [singleton] at h
    have hâ‚ : prod.map (ğŸ™ _) (b â‰« (Predicate.eq B)^) â‰« in_ B
    = prod.map (ğŸ™ _) (b' â‰« (Predicate.eq B)^) â‰« in_ B :=
      congrFun (congrArg CategoryStruct.comp (congrArg (prod.map (ğŸ™ B)) h)) (in_ B)
    rw [prod.map_id_comp, assoc, Power.comm, prod.map_id_comp, assoc, Power.comm] at hâ‚
    have comm : (b â‰« terminal.from _) â‰« t C
    = prod.lift b (ğŸ™ _) â‰« prod.map (ğŸ™ _) b â‰« Predicate.eq _ := by
      rw [terminal.comp_from, â†assoc, prod.lift_map, comp_id,
          id_comp, Predicate.lift_eq, Predicate.true_]
    rw [terminal.comp_from, hâ‚, â†assoc, prod.lift_map, id_comp, comp_id] at comm
    exact Predicate.eq_of_lift_eq comm.symm

/-- The predicate on `Pow B` which picks out the subobject of "singletons".
-/
def Predicate.isSingleton (B : C) : pow B âŸ¶ Î© C := Ï‡_ (singleton B)

/-- The name âŒˆÏ†âŒ‰ : âŠ¤_ C âŸ¶ Pow B of a predicate `Ï† : B âŸ¶ Î© C`.
This is the global element of `Pow B` associated to a predicate
on `B`.
-/
def name {B} (Ï† : B âŸ¶ Î© C) : âŠ¤_ C âŸ¶ pow B := (((prod.fst) â‰« Ï†))^

/-- Notation for `Name`. Consistent with Mac Lane and Moerdijk's notation. -/
notation "âŒˆ" Ï† "âŒ‰" => name Ï†

/-- The inverse of `Name`, sending a global element of `Pow B`
to the corresponding predicate on `B`.
-/
def Predicate.fromName {B} (Ï†' : âŠ¤_ C âŸ¶ pow B) : B âŸ¶ Î© C :=
  (prod.lift (ğŸ™ B) (terminal.from B)) â‰« Ï†'^

/-- The condition from the definition of `Name`
as the `P_transpose` of a morphism.
-/
lemma Predicate.NameDef {B} (Ï† : B âŸ¶ Î© C) : (prod.map (ğŸ™ _) âŒˆÏ†âŒ‰) â‰« (in_ B) = (prod.fst) â‰« Ï† :=
  comm _ _

/-- The equivalence between morphisms `B âŸ¶ Î© C` and morphisms `âŠ¤_ C âŸ¶ pow B`,
which comes from the more general equivalence between morphisms `B â¨¯ A âŸ¶ Î© C`
and morphisms `A âŸ¶ pow B`.
-/
def Predicate.NameEquiv (B : C) : (B âŸ¶ Î© C) â‰ƒ (âŠ¤_ C âŸ¶ pow B) where
  toFun := name
  invFun := fromName
  left_inv := by
    intro Ï†
    dsimp [name, fromName]
    rw [transpose_left_inv, â†assoc, prod.lift_fst, id_comp]
  right_inv := by
    intro Ï†'
    dsimp only [name, fromName]
    have h := (Limits.prod.rightUnitor B).hom_inv_id
    dsimp at h
    rw [â†assoc, h, id_comp, transpose_right_inv]

end
end CategoryTheory.Topos
