/-
Copyright (c) 2024 Charlie Conneen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Charlie Conneen
-/
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Topos.Power
import Topos.SubobjectClassifier

namespace CategoryTheory

open Category Limits Classifier Power

universe u v

variable (C : Type u) [Category.{v} C]

class Topos where
  [has_terminal : HasTerminal C]
  [has_pullbacks : HasPullbacks C]
  [subobject_classifier : HasClassifier C]
  [has_power_objects : HasPowers C]

attribute [instance] Topos.has_terminal Topos.has_pullbacks Topos.subobject_classifier Topos.has_power_objects

variable [Topos C] {C}

namespace Topos

noncomputable instance chosenFiniteProducts : ChosenFiniteProducts C := ChosenFiniteProducts.ofFiniteProducts C
instance hasBinaryProducts                  : HasBinaryProducts C    := hasBinaryProducts_of_hasTerminal_and_pullbacks C
instance hasFiniteProducts                  : HasFiniteProducts C    := hasFiniteProducts_of_has_binary_and_terminal
instance hasEqualizers                      : HasEqualizers C        := hasEqualizers_of_hasPullbacks_and_binary_products

noncomputable section

def Predicate.true_ (B : C) : B âŸ¶ Î© C := terminal.from B â‰« (t C)

/--
  The equality predicate on `B â¨¯ B`.
-/
def Predicate.eq (B : C) : B â¨¯ B âŸ¶ Î© C := ClassifierOfMono (diag B)

lemma Predicate.lift_eq {X B : C} (b : X âŸ¶ B) : prod.lift b b â‰« Predicate.eq B = Predicate.true_ X := by
  dsimp only [eq, true_]
  rw [â†prod.comp_diag b, assoc, (ClassifierMonoComm (diag B)), â†assoc, terminal.comp_from]

lemma Predicate.eq_of_lift_eq {X B : C} {b b' : X âŸ¶ B} (comm' : prod.lift b b' â‰« Predicate.eq B = Predicate.true_ X) : b = b' := by
  dsimp only [eq, true_] at comm'
  let cone_lift := ClassifierMonoCone_into (comm' := comm')
  have t : cone_lift â‰« diag _ = prod.lift b b' := ClassifierMonoCone_into_comm (comm' := comm')
  rw [prod.comp_diag] at t
  have tâ‚ := congrArg (fun k â†¦ k â‰« prod.fst) t
  have tâ‚‚ := congrArg (fun k â†¦ k â‰« prod.snd) t
  simp at tâ‚
  simp at tâ‚‚
  exact tâ‚.symm.trans tâ‚‚

/--
  The "singleton" map {â€¢}_B : B âŸ¶ Pow B.
  In Set, this map sends b âˆˆ B to the singleton set {b}.
-/
def singleton (B : C) : B âŸ¶ Pow B := P_transpose (Predicate.eq B)

/--
  `singleton B : B âŸ¶ Pow B` is a monomorphism.
-/
instance singletonMono (B : C) : Mono (singleton B) where
  right_cancellation := by
    intro X b b' h
    rw [singleton] at h
    have hâ‚ : prod.map (ğŸ™ _) (b â‰« P_transpose (Predicate.eq B)) â‰« in_ B = prod.map (ğŸ™ _) (b' â‰« P_transpose (Predicate.eq B)) â‰« in_ B :=
      congrFun (congrArg CategoryStruct.comp (congrArg (prod.map (ğŸ™ B)) h)) (in_ B)
    rw [prod.map_id_comp, assoc, Pow_powerizes, prod.map_id_comp, assoc, Pow_powerizes] at hâ‚
    have comm : (b â‰« terminal.from _) â‰« t C = prod.lift b (ğŸ™ _) â‰« prod.map (ğŸ™ _) b â‰« Predicate.eq _ := by
      rw [terminal.comp_from, â†assoc, prod.lift_map, comp_id, id_comp, Predicate.lift_eq, Predicate.true_]
    rw [terminal.comp_from, hâ‚, â†assoc, prod.lift_map, id_comp, comp_id] at comm
    exact Predicate.eq_of_lift_eq comm.symm

def Predicate.isSingleton (B : C) : Pow B âŸ¶ Î© C := ClassifierOfMono (singleton B)

/-- The name âŒˆÏ†âŒ‰ : âŠ¤_ C âŸ¶ Pow B of a predicate `Ï† : B âŸ¶ Î© C`. -/
def Name {B} (Ï† : B âŸ¶ Î© C) : âŠ¤_ C âŸ¶ Pow B := P_transpose (((prod.fst) â‰« Ï†))

def Predicate.fromName {B} (Ï†' : âŠ¤_ C âŸ¶ Pow B) : B âŸ¶ Î© C := (prod.lift (ğŸ™ B) (terminal.from B)) â‰« P_transpose_inv Ï†'

def Predicate.NameDef {B} (Ï† : B âŸ¶ Î© C) : (prod.map (ğŸ™ _) (Name Ï†)) â‰« (in_ B) = (prod.fst) â‰« Ï† :=
  Pow_powerizes _ _

def Predicate.NameEquiv (B : C) : (B âŸ¶ Î© C) â‰ƒ (âŠ¤_ C âŸ¶ Pow B) where
  toFun := Name
  invFun := fromName
  left_inv := by
    intro Ï†
    dsimp [Name, fromName]
    rw [P_transpose_left_inv, â†assoc, prod.lift_fst, id_comp]
  right_inv := by
    intro Ï†'
    dsimp only [Name, fromName]
    have h := (Limits.prod.rightUnitor B).hom_inv_id
    dsimp at h
    rw [â†assoc, h, id_comp, P_transpose_right_inv]

end
end Topos
end CategoryTheory
