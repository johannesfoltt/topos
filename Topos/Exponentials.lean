import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Closed.Cartesian
import Topos.Basic


namespace CategoryTheory

open Category Limits Classifier Power Topos

universe u v

variable {C : Type u} [Category.{v} C] [Topos C]

/-!
# Exponential Objects

Proves that a topos has exponential objects (internal homs).
Consequently, every topos is Cartesian closed.
-/


namespace Topos

noncomputable section

/-- The exponential object B^A. -/
def Hom (A B : C) : C :=
  pullback
    (P_transpose (P_transpose ((prod.associator _ _ _).inv â‰« in_ (B â¨¯ A)) â‰« Predicate.isSingleton B))
    (Name (Predicate.true_ A))

/-- The map which, in Set, sends a function (A â†’ B) âˆˆ B^A to its graph as a subset of B â¨¯ A. -/
def Hom_toGraph (A B : C) : Hom A B âŸ¶ Pow (B â¨¯ A) := pullback.fst

instance Hom_toGraph_Mono {A B : C} : Mono (Hom_toGraph A B) := pullback.fst_of_mono

lemma ExpConeSnd_Terminal (A B : C) : pullback.snd = terminal.from (Hom A B) := Unique.eq_default _

lemma Hom_comm (A B : C) : Hom_toGraph A B â‰« (P_transpose (P_transpose ((prod.associator _ _ _).inv â‰« in_ (B â¨¯ A)) â‰« Predicate.isSingleton B))
  = terminal.from (Hom A B) â‰« Name (Predicate.true_ A) := by
    rw [â†ExpConeSnd_Terminal]; exact pullback.condition

lemma evalDef_comm (A B : C) :
  (prod.map (ğŸ™ A) (Hom_toGraph A B) â‰« P_transpose ((prod.associator _ _ _).inv â‰« in_ (B â¨¯ A))) â‰« Predicate.isSingleton B
  = Predicate.true_ (A â¨¯ Hom A B) := by
    let id_m : A â¨¯ Hom A B âŸ¶ A â¨¯ Pow (B â¨¯ A) := prod.map (ğŸ™ _) (Hom_toGraph A B)
    let v := P_transpose ((prod.associator _ _ _).inv â‰« in_ (B â¨¯ A))
    let Ïƒ_B := Predicate.isSingleton B
    let u := P_transpose (v â‰« Predicate.isSingleton B)
    let id_u := prod.map (ğŸ™ A) u
    have comm_middle : v â‰« Ïƒ_B = id_u â‰« (in_ A) := Pow_powerizes A (v â‰« Ïƒ_B)
    have comm_left : id_m â‰« id_u =  prod.map (ğŸ™ _) (terminal.from _) â‰« prod.map (ğŸ™ _) (Name (Predicate.true_ A)) := by
      rw [prod.map_map, prod.map_map]
      ext; simp
      rw [prod.map_snd, prod.map_snd, Hom_comm]
    have h_terminal : (prod.map (ğŸ™ A) (terminal.from (Hom A B)) â‰« prod.fst) â‰« terminal.from A = terminal.from _ :=
      Unique.eq_default _
    rw [assoc, comm_middle, â†assoc, comm_left, assoc, â†Predicate.NameDef]
    dsimp [Predicate.true_]
    rw [â†assoc, â†assoc, h_terminal]

/-- The evaluation map eval : A â¨¯ B^A âŸ¶ B. -/
def eval (A B : C) : A â¨¯ (Hom A B) âŸ¶ B :=
  ClassifierCone_into (comm' := evalDef_comm A B)

lemma evalCondition (A B : C) : eval A B â‰« singleton B = prod.map (ğŸ™ _) (Hom_toGraph A B) â‰« P_transpose ((prod.associator _ _ _).inv â‰« in_ (B â¨¯ A)) :=
  ClassifierCone_into_comm _ _ _

abbrev Exponentiates {A B X HomAB : C}  (e : A â¨¯ HomAB âŸ¶ B) (f : A â¨¯ X âŸ¶ B) (f_exp : X âŸ¶ HomAB) :=
  (prod.map (ğŸ™ _) f_exp) â‰« e = f

structure IsExponentialObject {A B HomAB : C} (e : A â¨¯ HomAB âŸ¶ B) where
  exp : âˆ€ {X} (_ : A â¨¯ X âŸ¶ B), X âŸ¶ HomAB
  exponentiates : âˆ€ {X} (f : A â¨¯ X âŸ¶ B), Exponentiates e f (exp f)
  unique' : âˆ€ {X} {f : A â¨¯ X âŸ¶ B} {exp' : X âŸ¶ HomAB}, Exponentiates e f exp' â†’ exp f = exp'

class HasExponentialObject (A B : C) where
  HomAB : C
  e : A â¨¯ HomAB âŸ¶ B
  is_exp : IsExponentialObject e

variable (C)

class HasExponentialObjects where
  has_exponential_object : âˆ€ (A B : C), HasExponentialObject A B

variable {C}

attribute [instance] HasExponentialObjects.has_exponential_object

-- ## TODO
-- exhibit the type class instance `HasExponentialObjects C` for a topos `C`.

variable {A B X : C} (f : A â¨¯ X âŸ¶ B)

abbrev h_map : X âŸ¶ Pow (B â¨¯ A) := P_transpose ((prod.associator _ _ _).hom â‰« prod.map (ğŸ™ _) f â‰« Predicate.eq _)

lemma HomMapSquareComm :
  h_map f â‰« P_transpose (P_transpose ((prod.associator B A (Power.Pow (B â¨¯ A))).inv â‰« in_ (B â¨¯ A)) â‰« Predicate.isSingleton B) =
  terminal.from X â‰« Name (Predicate.true_ A) := by
    -- consider (1â¨¯f) â‰« (eq B) : B â¨¯ A â¨¯ X âŸ¶ Î© C.
    let id_f'eq : B â¨¯ A â¨¯ X âŸ¶ Î© C := prod.map (ğŸ™ _) f â‰« Predicate.eq _
    -- h is the map that, in `Set`, takes an element of X to the graph of the corresponding function.
    -- We want to lift this to a map X âŸ¶ Exp A B.
    -- The idea is to show that this map actually "maps elements of X to graphs of functions", which,
    -- in an arbitrary topos, is the same as checking commutativity of the obvious square.
    let h : X âŸ¶ Pow (B â¨¯ A) := P_transpose ((prod.associator _ _ _).hom â‰« id_f'eq)
    -- h is by definition a P-transpose
    have h_condition : (prod.associator _ _ _).hom â‰« id_f'eq = (prod.map (prod.map (ğŸ™ _) (ğŸ™ _)) h) â‰« in_ _ := by
      rw [prod.map_id_id]
      apply Pow_powerizes
    -- moving the associator to the rhs of `h_condition`.
    have h_conditionâ‚‚ : id_f'eq = (prod.associator _ _ _).inv â‰« (prod.map (prod.map (ğŸ™ _) (ğŸ™ _)) h) â‰« in_ _ := by
      rw [â†h_condition, â†assoc, (prod.associator _ _ _).inv_hom_id, id_comp]
    -- this is the map v: A â¨¯ P(Bâ¨¯A) âŸ¶ P(B) which was used in the definition of `Exp A B`.
    let v : A â¨¯ Pow (B â¨¯ A) âŸ¶ Pow B := P_transpose ((prod.associator _ _ _).inv â‰« in_ (B â¨¯ A))
    -- v is by definition a P-transpose
    have v_condition : (prod.associator _ _ _).inv â‰« in_ (B â¨¯ A) = prod.map (ğŸ™ _) v â‰« in_ _ := Pow_powerizes _ _
    have lhs : P_transpose (prod.map (ğŸ™ A) h â‰« v â‰« Predicate.isSingleton B) = h â‰« P_transpose (v â‰« Predicate.isSingleton B) := by
      apply Pow_unique
      dsimp only [Powerizes]
      rw [prod.map_id_comp, assoc _ _ (in_ A), â†Pow_powerizes _ _, â†assoc]
    rw [â†lhs]
    -- Claim that f â‰« {â€¢}_B = (1â¨¯h) â‰« v.
    -- This is obtained by showing that both maps are the P-transpose of (1â¨¯f) â‰« (eq B).
    -- There might be a slightly faster way to do this.
    have transposeâ‚ : P_transpose id_f'eq = f â‰« singleton _ := by
      apply Pow_unique
      dsimp only [Powerizes, Topos.singleton]
      rw [prod.map_id_comp, assoc, â†(Pow_powerizes B (Predicate.eq B))]
    have shuffle_h_around : (prod.associator B A X).inv â‰« (prod.map (prod.map (ğŸ™ _) (ğŸ™ _)) h) = prod.map (ğŸ™ _) (prod.map (ğŸ™ _) h) â‰« (prod.associator _ _ _).inv := by simp
    have transposeâ‚‚ : P_transpose id_f'eq = (prod.map (ğŸ™ _) h) â‰« v := by
      apply Pow_unique
      dsimp only [Powerizes]
      rw [h_conditionâ‚‚, â†assoc, shuffle_h_around, prod.map_id_comp, assoc _ _ (in_ B), â†v_condition, assoc]
    have eqnâ‚ : f â‰« singleton _ = (prod.map (ğŸ™ _) h) â‰« v := transposeâ‚.symm.trans transposeâ‚‚
    -- now compose by the `isSingleton B` predicate.
    have eqnâ‚‚ : f â‰« singleton _ â‰« Predicate.isSingleton _ = (prod.map (ğŸ™ _) h) â‰« v â‰« Predicate.isSingleton _ := by
      rw [â†assoc, â†assoc, eqnâ‚]
    rw [â†eqnâ‚‚]

    -- from here, the argument is mostly definition unpacking.
    apply Pow_unique
    dsimp only [Name, Predicate.true_, Powerizes, Predicate.isSingleton]
    have f_terminal : f â‰« terminal.from B = terminal.from _ := Unique.eq_default _
    have rightUnitor_terminal : (prod.rightUnitor A).hom â‰« terminal.from A = terminal.from _ := Unique.eq_default _
    have A_X_terminal : prod.map (ğŸ™ A) (terminal.from X) â‰« terminal.from (A â¨¯ âŠ¤_ C) = terminal.from _ := Unique.eq_default _
    have obv : terminal.from (A â¨¯ âŠ¤_ C) â‰« t C = prod.map (ğŸ™ A) (P_transpose (terminal.from (A â¨¯ âŠ¤_ C) â‰« t C)) â‰« in_ A := Pow_powerizes _ _
    rw [(Classifies (singleton _)).comm, â†assoc, f_terminal, â†assoc, rightUnitor_terminal, prod.map_id_comp, assoc, â†obv, â†assoc, A_X_terminal]

def Hom_map : X âŸ¶ Hom A B :=
  pullback.lift (h_map f) (terminal.from X) (HomMapSquareComm f)

@[simp]
lemma Hom_mapCondition : Hom_map f â‰« (Hom_toGraph A B) = h_map f :=
  pullback.lift_fst _ _ _

theorem Hom_Exponentiates : Exponentiates (eval A B) f (Hom_map f) := by
  dsimp only [Exponentiates]
  rw [â†cancel_mono (singleton B), assoc, evalCondition, â†assoc, prod.map_map, id_comp, Hom_mapCondition]
  have h : toPredicate (f â‰« singleton B) = toPredicate (prod.map (ğŸ™ A) (h_map f) â‰« P_transpose ((prod.associator B A (Power.Pow (B â¨¯ A))).inv â‰« in_ (B â¨¯ A))) := by
    rw [toPredicate, toPredicate, prod.map_id_comp, assoc, singleton, â†Pow_powerizes, prod.map_id_comp, assoc, â†Pow_powerizes, â†assoc]
    have h' : (prod.map (ğŸ™ B) (prod.map (ğŸ™ A) (h_map f)) â‰« (prod.associator B A (Power.Pow (B â¨¯ A))).inv)
      = (prod.associator B A X).inv â‰« (prod.map (ğŸ™ _) (h_map f)) := by simp
    rw [h', assoc, h_map, â†Pow_powerizes, â†assoc, Iso.inv_hom_id, id_comp]
  have hâ‚€ := congrArg (fun k => P_transpose k) h
  have tâ‚ : P_transpose (toPredicate (f â‰« singleton B)) = f â‰« singleton B := (transposeEquiv _ _).right_inv _
  have tâ‚‚ : P_transpose (toPredicate ((prod.map (ğŸ™ A) (h_map f) â‰« P_transpose ((prod.associator B A (Power.Pow (B â¨¯ A))).inv â‰« in_ (B â¨¯ A)))))
    = (prod.map (ğŸ™ A) (h_map f) â‰« P_transpose ((prod.associator B A (Power.Pow (B â¨¯ A))).inv â‰« in_ (B â¨¯ A))) :=
      (transposeEquiv _ _).right_inv _
  simp only [tâ‚, tâ‚‚] at hâ‚€
  exact hâ‚€.symm

theorem Hom_Unique : âˆ€ {exp' : X âŸ¶ Hom A B}, Exponentiates (eval A B) f exp' â†’ Hom_map f = exp' := by
  intro exp' h
  dsimp only [Exponentiates] at h
  have h_singleton := congrArg (fun k â†¦ k â‰« singleton B) h
  simp only at h_singleton
  let v : A â¨¯ Pow (B â¨¯ A) âŸ¶ Pow B := P_transpose ((prod.associator _ _ _).inv â‰« in_ (B â¨¯ A))
  -- want to rewrite (1â¨¯g) â‰« eval A B â‰« singleton B = (1â¨¯(gâ‰«m)) â‰« v
  have rhs : eval A B â‰« singleton B = prod.map (ğŸ™ _) (Hom_toGraph A B) â‰« v := by
    apply PullbackCone.IsLimit.lift_fst
  rw [assoc, rhs, â†assoc, â†prod.map_id_comp] at h_singleton
  let id_f'eq : B â¨¯ A â¨¯ X âŸ¶ Î© C := prod.map (ğŸ™ _) f â‰« Predicate.eq _
  have hâ‚ : P_transpose (id_f'eq) = f â‰« singleton B := by
    apply Pow_unique
    dsimp only [Powerizes, id_f'eq, singleton]
    rw [prod.map_id_comp, assoc, â†(Pow_powerizes _ (Predicate.eq B))]
  have hâ‚‚ : P_transpose (prod.map (ğŸ™ _) (prod.map (ğŸ™ _) (exp' â‰« Hom_toGraph A B)) â‰« (prod.associator _ _ _).inv â‰« in_ (B â¨¯ A))
    = prod.map (ğŸ™ _) (exp' â‰« Hom_toGraph A B) â‰« v := by
      apply Pow_unique
      dsimp only [Powerizes]
      nth_rewrite 2 [prod.map_id_comp]
      rw [assoc, â†(Pow_powerizes _ _)]
  have hâ‚ƒ := Pow_powerizes _ ((prod.map (ğŸ™ B) (prod.map (ğŸ™ A) (exp' â‰« Hom_toGraph A B)) â‰« (prod.associator B A (Power.Pow (B â¨¯ A))).inv â‰« in_ (B â¨¯ A)))
  dsimp only [Powerizes] at hâ‚ƒ
  rw [hâ‚‚, h_singleton, â†hâ‚, â†(Pow_powerizes _ id_f'eq), â†assoc] at hâ‚ƒ
  have h' := Hom_Exponentiates f
  dsimp only [Exponentiates] at h'
  have h'_singleton := congrArg (fun k â†¦ k â‰« singleton B) h'
  simp only at h'_singleton
  rw [assoc, rhs, â†assoc, â†prod.map_id_comp] at h'_singleton
  have hâ‚‚' : P_transpose (prod.map (ğŸ™ _) (prod.map (ğŸ™ _) (Hom_map f â‰« Hom_toGraph A B)) â‰« (prod.associator _ _ _).inv â‰« in_ (B â¨¯ A))
    = prod.map (ğŸ™ _) (Hom_map f â‰« Hom_toGraph A B) â‰« v := by
      apply Pow_unique
      dsimp only [Powerizes]
      nth_rewrite 2 [prod.map_id_comp]
      rw [assoc, â†(Pow_powerizes _ _)]
  have hâ‚ƒ' := Pow_powerizes _ ((prod.map (ğŸ™ B) (prod.map (ğŸ™ A) (Hom_map f â‰« Hom_toGraph A B)) â‰« (prod.associator B A (Power.Pow (B â¨¯ A))).inv â‰« in_ (B â¨¯ A)))
  dsimp only [Powerizes] at hâ‚ƒ'
  rw [hâ‚‚', h'_singleton, â†hâ‚, â†(Pow_powerizes _ id_f'eq), â†assoc] at hâ‚ƒ'
  have hx := hâ‚ƒ.trans hâ‚ƒ'.symm
  have câ‚€ : prod.map (ğŸ™ B) (prod.map (ğŸ™ A) (exp' â‰« Hom_toGraph A B)) â‰« (prod.associator _ _ _).inv
    = (prod.associator _ _ _).inv â‰« (prod.map (ğŸ™ _) (exp' â‰« Hom_toGraph A B)) := by simp
  have câ‚ : prod.map (ğŸ™ B) (prod.map (ğŸ™ A) (Hom_map f â‰« Hom_toGraph A B)) â‰« (prod.associator _ _ _).inv
    = (prod.associator _ _ _).inv â‰« (prod.map (ğŸ™ _) (Hom_map f â‰« Hom_toGraph A B)) := by simp
  rw [câ‚€, câ‚] at hx
  have hy := congrArg (fun k â†¦ (prod.associator B A X).hom â‰« k) hx
  simp only at hy
  rw [â†assoc, â†assoc, Iso.hom_inv_id, id_comp, â†assoc, â†assoc, Iso.hom_inv_id, id_comp] at hy
  have hz := congrArg (fun k â†¦ P_transpose k) hy
  simp only at hz
  rw [transposeEquiv.proof_3, transposeEquiv.proof_3] at hz
  rw [cancel_mono] at hz
  exact hz.symm


instance Hom_isExponential : IsExponentialObject (eval A B) where
  exp := Hom_map
  exponentiates := Hom_Exponentiates
  unique' := by apply Hom_Unique

instance ExponentialObject_inst (A B : C) : HasExponentialObject A B where
  HomAB := Hom A B
  e := eval A B
  is_exp := Hom_isExponential

instance ToposHasExponentials : HasExponentialObjects C where
  has_exponential_object := ExponentialObject_inst

variable (X Y Z W)

def InternalComposition : (Hom X Y) â¨¯ (Hom Y Z) âŸ¶ Hom X Z :=
  Hom_map ((prod.associator X (Hom X Y) (Hom Y Z)).inv â‰« (prod.map (eval X Y) (ğŸ™ _)) â‰« eval Y Z)

variable {X Y Z W}

def FnName (f : X âŸ¶ Y) : âŠ¤_ C âŸ¶ Hom X Y :=
  Hom_map (prod.fst â‰« f)

abbrev Hom_map_inv (f : X âŸ¶ Hom Y Z) := prod.map (ğŸ™ _) f â‰« eval _ _

def ExpAdjEquiv (A B X : C) : (A â¨¯ X âŸ¶ B) â‰ƒ (X âŸ¶ Hom A B) where
  toFun := Hom_map
  invFun := Hom_map_inv
  left_inv := fun f => Hom_Exponentiates f
  right_inv := by
    intro g
    apply Hom_Unique
    rw [Exponentiates]

variable (X Y)


def ExpHom {X Y : C} (A : C) (f : X âŸ¶ Y) : Hom A X âŸ¶ Hom A Y := Hom_map (eval A _ â‰« f)


def ExpFunctor (A : C) : C â¥¤ C where
  obj := fun B â†¦ Hom A B
  map := fun {X Y} f â†¦ ExpHom A f
  map_id := by
    intro X
    dsimp only [ExpHom]
    rw [comp_id]
    apply Hom_Unique
    dsimp only [Exponentiates]
    rw [prod.map_id_id, id_comp]
  map_comp := by
    intro X Y Z f g
    change ExpHom A (f â‰« g) = ExpHom A f â‰« ExpHom A g
    dsimp only [ExpHom]
    apply Hom_Unique
    dsimp only [Exponentiates]
    rw [prod.map_id_comp, assoc, Hom_Exponentiates, â†assoc, Hom_Exponentiates, assoc]

instance ToposMonoidal : MonoidalCategory C := monoidalOfHasFiniteProducts C

def TensorHomAdjunction (A : C) : MonoidalCategory.tensorLeft A âŠ£ ExpFunctor A := by
  apply Adjunction.mkOfHomEquiv
  fapply Adjunction.CoreHomEquiv.mk

  intro X B
  exact ExpAdjEquiv A B X

  intro X X' Y f g
  change prod.map (ğŸ™ _) (f â‰« g) â‰« eval _ _ = (prod.map (ğŸ™ _) f) â‰« prod.map (ğŸ™ _) g â‰« eval _ _
  rw [â†assoc, prod.map_map, id_comp]

  intro X Y Y' f g
  change Hom_map (f â‰« g) = Hom_map f â‰« ExpHom A g
  apply Hom_Unique
  dsimp only [Exponentiates, ExpHom]
  rw [prod.map_id_comp, assoc, Hom_Exponentiates, â†assoc, Hom_Exponentiates]

instance CartesianClosed : CartesianClosed C where
  closed := by
    intro B
    use ExpFunctor B
    exact TensorHomAdjunction B


end
end Topos
end CategoryTheory
